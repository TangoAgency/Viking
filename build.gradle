// Top-level build file where you can add configuration options common to all sub-projects/modules.
subprojects { project ->

  repositories {
    google()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://maven.google.com/" }
    maven { url "https://jitpack.io" }
  }

  apply plugin: 'checkstyle'

  checkstyle {
    configFile = rootProject.file('checkstyle.xml')
    toolVersion = "7.3"
  }

  task checkstyle(type: Checkstyle) {}

  afterEvaluate {
    if (project.tasks.findByName('check')) {
      check.dependsOn('checkstyle')
    }
  }

  buildscript {
    repositories {
      google()
      jcenter()
      mavenCentral()
      maven {
        url "https://plugins.gradle.org/m2/"
      }
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:3.3.0-rc03'
      //classpath 'gradle.plugin.com.kageiit:lintrules:1.1.2'
      //classpath 'com.novoda:bintray-release:0.8.1'
      classpath 'me.tatarka:gradle-retrolambda:3.7.0'
      classpath 'com.dicedmelon.gradle:jacoco-android:0.1.3'
    }
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    maven { url 'https://maven.google.com' }
    maven { url "https://jitpack.io" }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

ext {
  minSdkVersion = 15
  targetSdkVersion = 28
  compileSdkVersion = 28
  buildToolsVersion = '28.0.0rc1'
  sourceCompatibilityVersion = JavaVersion.VERSION_1_8
  targetCompatibilityVersion = JavaVersion.VERSION_1_8
}

def androidToolsVersion = '27.0.3'
def supportLibraryVersion = '27.0.2'
def googlePlayServicesVersion = '15.0.1'

allprojects {
  configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
      def requested = details.requested
      if (requested.group == 'com.android.support') {
        if (!requested.name.startsWith("multidex")) {
          details.useVersion supportLibraryVersion
        }
      }
    }
  }
}

ext.deps = [// Android
            android                          : 'com.google.android:android:4.1.1.4',
            supportCompat                    : "com.android.support:support-compat:$supportLibraryVersion",
            supportFragment                  : "com.android.support:support-fragment:$supportLibraryVersion",
            supportDesign                    : "com.android.support:design:$supportLibraryVersion",
            appCompat                        : "com.android.support:appcompat-v7:$supportLibraryVersion",
            supportAnnotations               : "com.android.support:support-annotations:$supportLibraryVersion",

            supportTestRunner                : 'com.android.support.test:runner:1.0.1',
            supportTestRules                 : 'com.android.support.test:rules:1.0.1',

            espressoCore                     : 'com.android.support.test.espresso:espresso-core:3.0.1',

            androidGradle                    : 'com.android.tools.build:gradle:3.2.1',


            lint                             : "com.android.tools.lint:lint:$androidToolsVersion",
            lintApi                          : "com.android.tools.lint:lint-api:$androidToolsVersion",
            lintChecks                       : "com.android.tools.lint:lint-checks:$androidToolsVersion",
            lintTests                        : "com.android.tools.lint:lint-tests:$androidToolsVersion",

            //Arch components
            archCompontentsImplementation    : "android.arch.lifecycle:extensions:1.0.0",
            archComponentsAnnotationProcessor: "android.arch.lifecycle:compiler:1.0.0",

            // Google Services
            googlePlayServicesMaps           : "com.google.android.gms:play-services-maps:$googlePlayServicesVersion",
            googleMapsUtils                  : "com.google.maps.android:android-maps-utils:0.5",

            //dagger
            dagger                           : 'com.google.dagger:dagger:2.16',
            daggerCompiler                   : 'com.google.dagger:dagger-compiler:2.16',
            daggerAndroid                    : "com.google.dagger:dagger-android:2.16",
            daggerAndroidSupport             : "com.google.dagger:dagger-android-support:2.16",
            daggerAndroidProccesor           : "com.google.dagger:dagger-android-processor:2.16",

            //RxJava
            rxJava                           : 'io.reactivex.rxjava2:rxjava:2.0.0',
            rxAndroid                        : 'io.reactivex.rxjava2:rxandroid:2.0.0',

            // Square
            javapoet                         : 'com.squareup:javapoet:1.7.0',

            // Google
            guava                            : 'com.google.guava:guava:25.1-jre',

            // Third Party
            javaparser                       : 'com.github.javaparser:javaparser-core:2.4.0',
            javax                            : 'javax.inject:javax.inject:1',

            // Test dependencies
            junit                            : 'junit:junit:4.12',
            mockito                          : 'org.mockito:mockito-core:2.7.5',
            truth                            : 'com.google.truth:truth:0.28',
            robolectric                      : 'org.robolectric:robolectric:3.2.2',
            compiletesting                   : 'com.google.testing.compile:compile-testing:0.9',
            autoservice                      : 'com.google.auto.service:auto-service:1.0-rc2',
            autocommon                       : 'com.google.auto:auto-common:0.6',]
